template("rust_crate") {
  action(target_name) {
    forward_variables_from(invoker,
                           [
                             "cfg",
                             "crate_type",
                             "source_root",
                             "deps",
                             "rust_deps",
                           ])
    sources = [
      source_root,
    ]
    outputs = []
    depfile = "$target_out_dir/$target_name.d"
    script = "//third_party/v8/tools/run.py"

    args = [
      "rustc",
      rebase_path(source_root, root_build_dir),
      "--crate-name=$target_name",
      "--crate-type=$crate_type",
      "--emit=dep-info=" + rebase_path(depfile, root_build_dir),
    ]

    # We only use staticlib for the special "empty" lib.
    if (crate_type == "staticlib") {
      staticlib = "$target_out_dir/$target_name.a"
      outputs += [ staticlib ]
      args += [ "--emit=link=" + rebase_path(staticlib, root_build_dir) ]
    }

    if (crate_type == "rlib" || crate_type == "bin") {
      obj = "$target_out_dir/$target_name.o"
      outputs += [ obj ]
      args += [ "--emit=obj=" + rebase_path(obj, root_build_dir) ]
    }

    if (crate_type == "rlib") {
      rlib = "$target_out_dir/lib$target_name.rlib"
      outputs += [ rlib ]
      args += [ "--emit=link=" + rebase_path(rlib, root_build_dir) ]
    }

    if (is_debug) {
      args += [ "-g" ]
    }

    if (is_official_build) {
      args += [ "-O" ]
    }

    if (defined(cfg)) {
      foreach(c, cfg) {
        args += [
          "--cfg",
          c,
        ]
      }
    }

    if (!defined(deps)) {
      deps = []
    }

    if (defined(rust_deps)) {
      deps += rust_deps
      foreach(dep_label, rust_deps) {
        dep_name = get_label_info(dep_label, "name")
        dep_dir = get_label_info(dep_label, "target_out_dir")
        dep_rlib = "$dep_dir/lib$dep_name.rlib"
        args += [
          "--extern",
          "$dep_name=" + rebase_path(dep_rlib, root_build_dir),
        ]
      }
    }
  }
}

template("rust_component") {
  crate_target = target_name + "_crate"
  rust_crate(crate_target) {
    forward_variables_from(invoker,
                           [
                             "crate_type",
                             "rust_deps",
                             "source_root",
                           ])
    if (!defined(invoker.crate_type)) {
      crate_type = "rlib"
    }
  }

  crate_outputs = get_target_outputs(":" + crate_target)
  component(target_name) {
    forward_variables_from(invoker,
                           [
                             "libs",
                             "deps",
                           ])
    if (!defined(libs)) {
      libs = []
    }
    libs += [ crate_outputs[0] ]
    deps = [
      ":" + crate_target,
    ]
  }
}

template("rust_library") {
  rust_crate(target_name) {
    crate_type = "rlib"
    forward_variables_from(invoker, "*")
  }
}

template("rust_executable") {
  bin_name = target_name + "_bin"
  bin_target = ":" + bin_name
  rust_crate(bin_name) {
    crate_type = "bin"
    forward_variables_from(invoker, "*")
  }

  # By compiling an empty file as crate-type=staticlib we get all the code
  # for the rust stdlib, which are not included in the object file outputs
  # of other libs.
  stdlib_target = "//build_extra/rust:stdlib"

  executable(target_name) {
    forward_variables_from(invoker, "*")

    if (!defined(deps)) {
      deps = []
    }

    deps += [
      bin_target,
      stdlib_target,
    ]

    libs = [
      get_label_info(bin_target, "target_out_dir") + "/$bin_name.o",
      get_label_info(stdlib_target, "target_out_dir") + "/stdlib.a",
    ]

    if (defined(rust_deps)) {
      deps += rust_deps
      foreach(dep_label, rust_deps) {
        dep_name = get_label_info(dep_label, "name")
        dep_dir = get_label_info(dep_label, "target_out_dir")
        dep_obj = "$dep_dir/$dep_name.o"
        libs += [ dep_obj ]
      }
    }
  }
}
